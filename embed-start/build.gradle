plugins {
    id 'java'
}

group = 'hello'
version = '0.0.1-SNAPSHOT'
sourceCompatibility = '17'

repositories {
    mavenCentral()
}

dependencies {
    //스프링 MVC 추가
    implementation 'org.springframework:spring-webmvc:6.0.4'

    //내장 톰켓 추가
    implementation 'org.apache.tomcat.embed:tomcat-embed-core:10.1.5'
}

tasks.named('test') {
    useJUnitPlatform()
}

//일반 Jar 생성
task buildJar(type: Jar) {
    manifest {
        attributes 'Main-Class': 'hello.embed.EmbedTomcatSpringMain'
    }
    with jar
}

//Fat Jar 생성
/**
 * jar안에는 jar를 포함하지 못해, jar안의 class파일들을 추출해 포함시킨다.
 * 이로 인해 WAR의 단점을 해결했다.
 * WAS를 별도로 설치하지 않아도 된다. 톰캣같은 WAS가 라이브러리로 jar 내부에 포함되어 있다.
 * 그러나 다 풀려서 class파일로 들어있기 때문에 어떤 라이브러리가 포함되어 있는지 확인하기가 어렵다.
 * 또한 파일명 중복을 해결할 수가 없다.
 *
 */
task buildFatJar(type: Jar) {
    manifest {
        attributes 'Main-Class': 'hello.embed.EmbedTomcatSpringMain'
    }
    duplicatesStrategy = DuplicatesStrategy.WARN
    from { configurations.runtimeClasspath.collect { it.isDirectory() ? it : zipTree(it) } }
    with jar
}
